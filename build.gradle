buildscript {
    apply from: project.rootProject.relativePath('buildscript.gradle'), to: buildscript
}

apply plugin: 'osgi-container'

allprojects {
    group 'com.neva.javarel'
    version "1.0.0.ALPHA"

    apply plugin: 'idea'
    apply plugin: 'base'

    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects { subProject ->
    plugins.withId 'osgi-module', {
        jar {
            manifest {
                name subProject.description
                instruction 'Bundle-Category', 'javarel'
            }
        }

        dependencies {
            osgiBundle 'org.jetbrains.kotlin:kotlin-osgi-bundle:1.0.0'

            osgiBundle 'org.osgi:org.osgi.enterprise:5.0.0'
            osgiBundle 'org.osgi:org.osgi.core:5.0.0'

            osgiBundle 'javax.servlet:javax.servlet-api:3.0.1'

            osgiBundle 'com.google.guava:guava:19.0'
            osgiBundle 'com.google.code.gson:gson:2.6.2'
            osgiBundle 'commons-io:commons-io:2.4'
            osgiBundle 'org.apache.commons:commons-lang3:3.4'
            osgiBundle 'org.slf4j:slf4j-api:1.7.6'

            compile 'org.apache.felix:org.apache.felix.scr.annotations:1.9.12'
        }
    }

    plugins.withId 'kotlin', {
        // testCompile group: 'junit', name: 'junit', version: '4.11'

        sourceCompatibility = 1.7
        targetCompatibility = 1.7

        def compileOptions = {
            options.encoding = "UTF-8"
        }

        compileJava compileOptions
        compileTestJava compileOptions
    }

    plugins.withId 'com.twcable.scr', {
        processScrAnnotations {
            skipInterfaceReferenceCheck = true
        }
    }
}

dependencies {
    osgiMain 'org.apache.felix:org.apache.felix.main:5.4.0'
    osgiBundle 'org.apache.felix:org.apache.felix.configadmin:1.8.8'
    osgiBundle 'org.apache.felix:org.apache.felix.eventadmin:1.4.6'

    osgiBundle 'org.apache.felix:org.apache.felix.metatype:1.1.2'

    osgiBundle 'org.apache.felix:org.apache.felix.webconsole:4.2.14'
    osgiBundle 'org.apache.felix:org.apache.felix.log:1.0.1'

    osgiBundle 'org.apache.felix:org.apache.felix.scr:2.0.2'

    osgiBundle 'org.ops4j.pax.logging:pax-logging-service:1.8.5'

    // TODO switch to pax-web / newest Jersey, but binding need to be fixed
    osgiBundle 'org.apache.felix:org.apache.felix.http.bundle:2.3.2'
    // osgiBundle 'org.ops4j.pax.web:pax-web-api:4.2.5'
    // osgiBundle 'org.ops4j.pax.web:pax-web-spi:4.2.5'
    // osgiBundle 'org.ops4j.pax.web:pax-web-runtime:4.2.5'
    // osgiBundle 'org.ops4j.pax.web:pax-web-jetty-bundle:4.2.5'
    //osgiBundle 'org.apache.xbean:xbean-finder:4.5'

    osgiBundle 'org.slf4j:slf4j-log4j12:1.7.6'
    osgiBundle 'commons-fileupload:commons-fileupload:1.3.1'
    osgiBundle 'commons-io:commons-io:2.4'
    osgiBundle 'org.json:json:20160212'

    osgiModule project(':fragment:felix-webconsole')

    osgiModule project(':foundation')
    osgiModule project(':resource')
    osgiModule project(':communication:rest')
    osgiModule project(':presentation:asset')
    osgiModule project(':presentation:view')
    osgiModule project(':presentation:view:pebble')
    osgiModule project(':storage:database')
    osgiModule project(':storage:repository')
    osgiModule project(':processing:scheduler')

    osgiModule project(':app:core')
    osgiModule project(':app:adm')
}

osgiContainer {
    felix()
    debug()
}